use std.io.base std.backend
private{
 const{
  [] char 2 READ $r,$\0
  [] char 2 WRITE $w,$\0
 }
 fn void checkErrNo int num String path{
    if num == -1{
       var msg getErrNoMessage
       IOException:throw String:new msg,getCStringLen msg;,false; + " "; + path
     }
 }
}
abstract class FileHandler Closeable{
   protected{
     const void* handle
     fn void checkError{
        var num fileError handle
        if num != 0{
          var msg getErrorMessage num
          IOException:throw String:new msg,getCStringLen msg;,false
        }
      }
   }
   this String name char* const mode{
      var cname name toCString
      handle = fileOpen cname,mode
      cname free
      if handle == nil{
        var msg getErrNoMessage
        IOException:throw String:new msg,getCStringLen msg;,false; + " "; + name
      }
   }
   this void* handle = this handle = handle
   override fn void close{
     fileClose handle
     checkError
   }
}
class FileWriter FileHandler Writable{
  this String name:super name,WRITE
  this void* handle:super handle
  override fn void write byte* p memory size{
    fileWrite handle,p,size
    checkError
  }
  fn void flush = fileFlush handle
}
class FileReader FileHandler Readable{
  this String name:super name,READ
  this void* handle:super handle
  override fn void read byte* p memory size{
    fileRead handle,p,size
    checkError
  }
  override fn bool ready = fileEOF handle; !
}
class DirectoryStream{
  private{
    const void* handle
    DirectoryEntry* last nil
  }
  this String name{
    var cstr name toCString
    handle = directoryOpen cstr
    cstr free
    if handle == nil{
      var msg getErrNoMessage
      IOException:throw String:new msg,getCStringLen msg;,false; + " "; + name
    }
  }
  fn String read{
    var entry last
    last = nil
    if entry == nil{
      entry = directoryRead handle
      if entry == nil{
        IOException:throw "no more files"
      }
    }
    var nameLen getCStringLen entry name
    String name init nameLen
    for i,nameLen{
      name set i,entry name get i
    }
    return name
  }
  fn bool ready{
    if last == nil{
     last = directoryRead handle
    }
    return last != nil
  }
  fn long tell = directoryTell handle
  fn void seek long loc = directorySeek handle,loc
  fn void reset = directoryReset handle
  fn void close{
    if directoryClose handle; == -1{
      var msg getErrNoMessage
      IOException:throw String:new msg,getCStringLen msg;,false
    }
  }
}
fn bool isDirectory String path{
  FileStatus status
  checkErrNo fileStatus path toCString;,status ref;,path
  return std.backend.isDirectory status ref
}
fn bool isFile String path{
 FileStatus status
 checkErrNo fileStatus path toCString;,status ref;,path
 return std.backend.isFile status ref
}
fn memory getSize String path{
 FileStatus status
 checkErrNo fileStatus path toCString;,status ref;,path
 return getFileSize status ref
}
const compIf os WINDOWS{
   var PATH_SEPARATOR_CHAR $; ; SEPARATOR_CHAR $\\;SEPARATOR "\\"; PATH_SEPARATOR ";"
}{
   var PATH_SEPARATOR_CHAR $: ; SEPARATOR_CHAR $/;SEPARATOR "/"; PATH_SEPARATOR ":"
}